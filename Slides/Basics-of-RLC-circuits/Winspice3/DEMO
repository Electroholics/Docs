* demo control file
.control

********************************************
* note: all commands need to be lower case *
********************************************

unset slowplot
source demo.cir
* the save command needs to be before the ac or tran commands
* save data from only node 1 (the input node) and out
save 1 out

* run will do the .analysis as listed in the input file
run
plot db(out) xlog
source demo1.cir

* to add vin's current to the list of saved vectors
* (no need to repeat nodes 1 and out)
save vin#branch 

echo AC analysis
ac dec 20 1 1meghz
plot db(out) xlog
echo transient analysis of filter (pulse input)
tran 100us 6000us
plot v(1) out

source demo2.cir
echo AC analysis
ac dec 15 1 100meghz
plot db(out) xlog


echo Transient analysis with sine input (to be used with fourier analysis)

tran .5us 200us
plot v(1) out
fourier 10k out

echo End of demo
echo
.endc
